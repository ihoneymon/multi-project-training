plugins {
    id 'org.springframework.boot' version '2.0.2.RELEASE'
}

def requiredIntegrationProjects = [project(":b-api"), project(":c-app")]

group = 'io.honeymon.springboot'

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    def javaVer = '1.8'

    targetCompatibility = javaVer
    sourceCompatibility = javaVer
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.springframework.boot:spring-boot-starter"
    }
}

project(':a-common') {
    description = "Common project"
    dependencies {
        compile "org.projectlombok:lombok"
    }

    bootJar.enabled = false
}

project(':b-api') {
    description = "API preojct"

    dependencies {
        compile project(':a-common')

        compile "org.springframework:spring-web"
    }

    bootJar.enabled = false
}

project(':c-app') {
    apply plugin: 'org.springframework.boot'
    description = "Application project"

    dependencies {
        compile project(':b-api')

        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.springframework.boot:spring-boot-starter-logging"
    }
}

configure(requiredIntegrationProjects) {
    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/java')
            }
            resources {
                srcDir file('src/integration-test/resources')
            }
        }
    }

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    task integrationTest(type: Test) {
        description = "Runs then integration tests."

        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test


    dependencies {
        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}